import java.nio.file.Files

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.5.0-rc-2'
    }
}

task jmh {
    // NOTE this is not the jmh task from the jmh plugin.  It is merely a "grouping task"
    // for things to do when the benchmarks are run from the root - mainly creating a
    // unified report

    doLast {
        getLogger().lifecycle( "Generating shared JMH result report" );

		// todo : generate the report :)
    }
}

// NOTE : the benchmark project also define a `benchmark` alias for the `jmh`
// task.  Allow that to be run from the root as well.
task benchmark( dependsOn: [tasks.jmh] )

allprojects {
    group 'org.hibernate.sebersole.benchmarks'
    version '1.0-SNAPSHOT'
}

final String GRADLEW = 'gradlew'
final String GRADLEW_BAT = GRADLEW + '.bat'

wrapper {
    gradleVersion = '5.6.2'
    distributionType = Wrapper.DistributionType.ALL

    doLast {
        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        // Create gradlew links for each sub-project in its project dir

        final File realWrapperScriptFile = rootProject.file( GRADLEW )
        final File realWrapperBatScriptFile = rootProject.file( GRADLEW_BAT )

        subprojects { Project sub ->

            // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
            // *nix (yes, Mac is linux based) - create a symlink to the "real" gradlew

            def realWrapperRelativePath =  sub.relativePath( realWrapperScriptFile )
            def symLinkRef = new File( sub.projectDir, GRADLEW )
            def symlinkPath = symLinkRef.toPath()

            Files.deleteIfExists( symlinkPath )
            Files.createSymbolicLink( symlinkPath, sub.file( realWrapperRelativePath ).toPath() );


            // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
            // Windows - create a relative file that calls the "real" gradlew

            def realWrapperBatRelativePath =  sub.relativePath( realWrapperBatScriptFile )
            def wrapperBatLinkFile = new File( sub.projectDir, GRADLEW_BAT )

            if ( ! wrapperBatLinkFile.exists() ) {
                wrapperBatLinkFile.createNewFile()
            }

            wrapperBatLinkFile.text = "call \"${realWrapperBatRelativePath}\""
            wrapperBatLinkFile.executable = true
        }
    }
}